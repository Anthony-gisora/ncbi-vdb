set ( UNIX_BASE_SRC unix/misc.c unix/syslog.c unix/systime.c unix/syswriter.c )
set ( UNIX_SRC ${UNIX_BASE_SRC} unix/syserrcode.c )
set ( LINUX_SRC ${UNIX_BASE_SRC} linux/syserrcode.c )
set ( MAC_SRC ${UNIX_SRC} )
set ( WIN_SRC win/misc.c win/sysalloc.c win/syserrcode.c win/syslog.c win/systime.c win/syswriter.c )

if ( "win" STREQUAL ${OS} )
    set ( OS_SPECIFIC_SRC ${WIN_SRC} )
elseif( "linux" STREQUAL ${OS} )
    set ( OS_SPECIFIC_SRC ${LINUX_SRC} )
elseif( "mac" STREQUAL ${OS} )
    set ( OS_SPECIFIC_SRC ${MAC_SRC} )
endif()

if( FLEX_FOUND AND BISON_FOUND )
    FLEX_TARGET( JsonFlex json-lex.l ${CMAKE_CURRENT_SOURCE_DIR}/json-lex.c
        COMPILE_FLAGS "--debug --noline" )
    set( JsonScanner ${FLEX_JsonFlex_OUTPUTS} )
    BISON_TARGET( JsonGrammar json-grammar.y ${CMAKE_CURRENT_SOURCE_DIR}/json-grammar.c
        COMPILE_FLAGS "-Wno-other --no-lines -r state" )
    set( JsonParser ${BISON_JsonGrammar_OUTPUT_SOURCE} )
    ADD_FLEX_BISON_DEPENDENCY(JsonFlex JsonGrammar)
else()
    set( JsonScanner json-lex.c )
    set( JsonParser json-grammar.c )
endif()

if (CMAKE_BUILD_TYPE STREQUAL "Debug")
    set( BUILD_SPECIFIC_SRC debug.c )
else()
    set( BUILD_SPECIFIC_SRC release.c )
endif (CMAKE_BUILD_TYPE STREQUAL "Debug")

add_subdirectory( judy )

add_library(klib STATIC
    all-vdb-version
    base64
    bsearch
    bstpersist #TODO: bstpersist.nopt
    btree
    container
    cp1252
    crc32
    data-buffer
    guid
    hashfile
    hashtable
    iso8859-1
    iso8859
    ${JsonParser}
    ${JsonScanner}
    json
    judy-vector
    ksort
    log
    md5
    namelist
    num-gen
    out
    pack
    # NOTE:
    # pbstree-impl.nopt pbstree-native.c ptpersist.nopt ptrie.nopt
    # were generated by the old gmake build but are not used anywhere, apparently
    pbstree-impl
    pbstree-swapped.c # was 'pbstree-impl.swap.nopt' in the old gmake build
    pbstree
    #
    printf
    progressbar
    ptpersist
    ptrie
    qsort
    rc-idx-tbl
    rc-tbl
    refcount
    report-klib
    SHA-32bit
    SHA-64bit
    sra-release-version
    status-rc-strings
    status-rc
    status
    symtab
    text
    time
    token
    trie
    unpack
    utf8
    vector
    vector_namelist
    vlen-encode
    writer
    ${BUILD_SPECIFIC_SRC}
    ${OS_SPECIFIC_SRC}
)

add_compile_definitions (__mod__="libs/klib")
add_compile_definitions (_ARCH_BITS=${BITS})
include_directories(BEFORE ${CMAKE_CURRENT_SOURCE_DIR} )
